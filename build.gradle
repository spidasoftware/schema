apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'groovy'

group = 'com.spidasoftware'
version = '0.2-SNAPSHOT'
description = "Schema"

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets {
	main {
		groovy {
			srcDir 'src/main/groovy'
		}
		resources {
			srcDir 'resources'
		}
	}
	test {
		groovy {
			srcDir 'src/test/groovy'
		}
		resources {
			srcDir 'src/test/resources'
		}
	}
}

repositories {
	mavenCentral()
	maven { url "https://www.spidasoftware.com/artifactory/exposed-repo" }
}

dependencies {
	compile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.1.7'
	compile group: 'junit', name: 'junit', version:'4.9'
	compile group: 'net.tjws', name: 'app', version:'1.98'
	compile group: 'net.tjws', name: 'conn_chk', version:'1.98'
	compile group: 'net.tjws', name: 'jasper', version:'1.98'
	compile group: 'net.tjws', name: 'jsp', version:'1.98'
	compile group: 'net.tjws', name: 'servlet', version:'1.98'
	compile group: 'net.tjws', name: 'war', version:'1.98'
	compile group: 'net.tjws', name: 'webserver', version:'1.99'
	compile group: 'org.glassfish', name: 'javax.servlet', version:'3.0'
	compile group: 'net.sf.json-lib', name: 'json-lib', version:'2.3', classifier:'jdk15'
	compile group: 'log4j', name: 'log4j', version:'1.2.17'
	compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.0'
	compile group: 'xom', name: 'xom', version:'1.2.5'
	compile group: 'com.github.fge', name: 'json-schema-validator', version:'2.1.6'
}

def csvFile = hasProperty('file') ? file : null
def schema = hasProperty('schema') ? schema: null
def jsonFile = hasProperty('jsonFile') ? jsonFile: null

task(csv, dependsOn: 'classes', type: JavaExec) {
	if(csvFile){
		def file = new File("$csvFile")
		args file.absolutePath
	}
	main = 'com.spidasoftware.schema.utils.PoleCSVStationConverter'
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
	standardOutput = System.out
}

task(validateJson, dependsOn: 'classes', type: JavaExec) {
	if (jsonFile && schema) {

		def jFile = new File("$jsonFile")
		args schema
		args jFile.absolutePath
	}
	main = 'com.spidasoftware.schema.validation.CommandLineValidator'
	classpath = sourceSets.main.runtimeClasspath
	standardInput = System.in
	standardOutput = System.out
}

def getMavenSettings = {
	String userHome = System.getProperty("user.home");
	File mavenSettings = new File(userHome, ".m2/settings.xml")
	def xmlSlurper = new XmlSlurper()
	def root = xmlSlurper.parse(mavenSettings)
	def settingsMap = [:]
	
	def locations = root."profiles"."profile"[0]."properties"
	settingsMap.snapshots = locations."snapshotsLocation"
	settingsMap.releases = locations."releasesLocation"
	
	def serverElement = root."servers"."server".find{ it."id".text() == "spida" }
	settingsMap.username = serverElement.username.text()
	settingsMap.password = serverElement.password.text()
	return settingsMap
}

uploadArchives {
	def mavenSettings = getMavenSettings()
	repositories.mavenDeployer {
		repository(url: mavenSettings["releases"]) {
			authentication(userName: mavenSettings["username"], password: mavenSettings["password"])
		}
		snapshotRepository(url: mavenSettings["snapshots"]) {
			authentication(userName: mavenSettings["username"], password: mavenSettings["password"])
		}
	}
}
